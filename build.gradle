buildscript {
    ext.kotlin_version = '1.8.21'
    ext.versions = [
            'java'               : JavaVersion.VERSION_17,
            'androidGradlePlugin': '8.0.1',
            'googleServices'     : '4.3.15',
            'compileSdk'         : 33,
            'buildTools'         : '33.0.2',
            'minSdk'             : 28,
            'targetSdk'          : 33,
            'supportLibrary'     : '26.1.0',
            'constraintLayout'   : '1.1.3',
            'firebase'           : '10.0.1',
            'retrofit'           : '2.0.0-beta4',
            'okhttp'             : '3.6.0',
            'ion'                : '2.1.8',
            'videoAndroid'       : '7.6.1',
            'audioSwitch'        : '1.1.7'
    ]

    ext.getSecretProperty = { key, defaultValue ->
        def value = System.getenv(key)

        if (value == null) {
            Properties properties = new Properties()
            if (project.rootProject.file('local.properties').exists()) {
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                value = properties.getProperty(key)
            }
        }

        if (value == null) {
            value = defaultValue
        }

        return value
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidGradlePlugin"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}
